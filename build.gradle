plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

// Reproducible builds
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

apply plugin: 'java'

// Defines a Java 8 compatible JDK.
def compiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(8)
}

// Tells Gradle to use the previously defined Java 8 compatible JDK.
java {
    toolchain {
        compiler
    }
}

// Tells Gradle to always compile with the previously defined Java 8 compatible JDK.
tasks.withType(JavaCompile).configureEach {
    javaCompiler = compiler
}

tasks.withType(JavaCompile) {
    // Ensures that the encoding of source files is set to UTF-8, see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation ('com.github.KaptainWutax:BiomeUtils:-SNAPSHOT'){
        transitive = false
    }
    implementation ('com.github.KaptainWutax:SeedUtils:-SNAPSHOT'){
        transitive = false
    }
    implementation ('com.github.KaptainWutax:MathUtils:-SNAPSHOT'){
        transitive = false
    }
    implementation("com.google.guava:guava:30.1-jre")
}

// Package dependencies with Shadow plugin
shadowJar {
    minimize()
    mainClassName = 'randomreverser.PigSpawnerFinder' 
}

application {
    mainClass = 'randomreverser.PigSpawnerFinder' 
}

jar {
    manifest {
        attributes (
            'Main-Class': 'randomreverser.PigSpawnerFinder'
        )
    }
}
